; File with functionality for setting up the environment

; FILE HISTORY:

; 2023-04-10 Initial template, gks
; 2023-04-13 Added factory tiles, Group 8 (Ingvar Pétursson, Hampus Oxenholt, Adam Makowski)
; 2023-04-14 Added bar, Group 8 (Ingvar Pétursson, Hampus Oxenholt, Adam Makowski)
; 2023-04-27 need to add the comments here Group 4 (Anton, Marcus & Andreas)
; 2023-05-01 Fixed issue in create-building, now all global locations are updated instead of only town square, Group 8 (Ingvar Pétursson, Hampus Oxenholt, Adam Makowski)
; 2023-05-11 Added policeStation functionality, Group 2 (Alameer Al-Badrani, Samuel Peltomaa Åström, Simon Winoy, Sinan Sabree)
; 2023-05-18 Group 4(Anton, Andreas, Marcus)
; - Townswquare no longer has a doorway assigned to it in create-building.
;---------------------------------------------------


; ******************* SETUP ENVIRONMENT *****************
;
;---- General setup of the model and the regions
;- use Model Settings under 'Interface'->'Settings...'
;
; World
; location of origin: Corner
; Bottom Left
; min-pxcor: 0 (minimum x coordinate for patches)
; max-pxcor: 66 (maximum x coordinate for patches)
; min-pycor: 0 (minimum y coordinate for patches)
; max-pycor: 33 (maximum y coordinate for patches)
; X world wraps horizontally
; X world wraps vertically
; View
; Patch size: 22.12
; Font size: 10
; Frame rate: 30
;
; X show tick counter
; Tick counter label: ticks
;-------------------------------------------------

;---- Procedure
to setup-environment
  ask patches [
    set region "empty"
    set doorway false
  ]
  
  ;---- Place for volunteers
  create-building 1 1 3 violet "volunteerPlace"
  
  ;---- Evening entertainment place
  create-building 1 2.5 2 pink "eveningEntertainment"
  
  ;---- Factory
  create-building 1 3.5 6 brown "factory"
  
  ;---- Work place
  create-building 1 2 3 yellow "workPlace"
  
  ;---- Town Square
  ;create-building 2 2 10 green "townSquare"
  create-building 2 2 20 green "townSquare"
  
  ;---- University
  create-building 2 3.5 4 magenta "university"
  
  ;---- Restaurants
  create-building 3 1 2 cyan "restaurant"
  

  ;---- Police Station
  create-building 3 2 4 96 "policeStation"
  
  ask cops [
    let tempPatch one-of patches with [region = "policeStation"]
    setxy [pxcor] of tempPatch [pycor] of tempPatch
  ]
  
  ;---- Prison
  create-building 3 3 8 gray "prison"
  
  ;---- Houses
  let emptyPatches patches with [region = "empty"]
  let tempPatch 0
  let counter 0
  let agentID counter
  loop [
    set tempPatch one-of emptyPatches with [region = "empty"]
    
    ask tempPatch [
      set region "house"
      set pcolor 74
    ]
    
    ask citizens with [who = counter][
      set homeLoc tempPatch
      setxy [pxcor] of tempPatch [pycor] of tempPatch
    ]
    
    set counter counter + 1
    if count citizens = counter [stop]
  ]
  
  
  ;---- setup of environment-variables
  ;- time
  set flagMorning true
  set flagEvening false
  set flagWeekend false
  set time 0
  
  
end ;- setup environment

to create-building [x y buildingSize buildingColor regionName]
  
  let building-half-size buildingSize / 2
  let building-xcor (max-pxcor / 4) * x - (building-half-size)
  let building-ycor (max-pycor / 4) * y - (building-half-size)

  ask patches with [
    pxcor >= building-xcor and
    pxcor < building-xcor + buildingSize and
    pycor >= building-ycor and
    pycor < building-ycor + buildingSize
  ] [
    set pcolor buildingColor
    set region regionName
  ]
  if not (regionName = "townSquare") [
    ask patches with [
      pxcor = round (building-xcor) and
      pycor = round (building-ycor + building-half-size)
    ] [
      set pcolor red
      set doorway true
    ]
  ]
  
  if showPatchLabels [
    ask patches with [
      pxcor = round (building-xcor + building-half-size) and
      pycor = round (building-ycor + building-half-size)
    ] [
      set plabel regionName
    ]
  ]
  

  ; Set the global variable to the location of the building. 
  if regionName = "prison" [ set locPrison patch building-xcor building-ycor ]
  if regionName = "factory" [ set locFactory patch building-xcor building-ycor ]
  if regionName = "workPlace" [ set locWork patch building-xcor building-ycor ]
  if regionName = "townSquare" [ set locTownSquare patch building-xcor building-ycor ]
  if regionName = "university" [ set locUni patch building-xcor building-ycor ]
  if regionName = "restaurant" [ set locRestaurant patch building-xcor building-ycor ]
  if regionName = "policeStation" [ set locPolStation patch building-xcor building-ycor ]
  if regionName = "volunteerPlace" [ set locVolunteerPlace patch building-xcor building-ycor ]
  if regionName = "eveningEntertainment" [ set locEntertainment patch building-xcor building-ycor ]
  
  
end

